
Перем Интерфейс Экспорт;
Перем ГлавноеОкно;
Перем Холст;
Перем ЭлементФормыКартинка;
Перем Изображение;

Процедура ПриСозданииОбъекта()
	ПодключитьВнешнююКомпоненту("src/Классы/OneScriptForms.dll");

	Интерфейс = Новый ФормыДляОдноСкрипта();

КонецПроцедуры

Функция ГлавноеОкно(Заголовок = "", ШиринаОкна = 800, ВысотаОкна = 600) Экспорт

	Если ГлавноеОкно = Неопределено Тогда

		ГлавноеОкно = Интерфейс.Форма();
		ГлавноеОкно.МаксимальныйРазмер = Интерфейс.Размер(ШиринаОкна, ВысотаОкна);
		ГлавноеОкно.Высота = ВысотаОкна;
		ГлавноеОкно.Ширина = ШиринаОкна;
		ГлавноеОкно.Текст = Заголовок;
		ГлавноеОкно.ДвойнаяБуферизация = Истина;

		ГлавноеОкно.Отображать = Истина;
		ГлавноеОкно.Показать();
		ГлавноеОкно.Активизировать();

		ЭлементФормыКартинка = ГлавноеОкно.ЭлементыУправления.Добавить(Интерфейс.ПолеКартинки());
		ЭлементФормыКартинка.Стыковка = Интерфейс.СтильСтыковки.Заполнение;
		ЭлементФормыКартинка.Ширина = ГлавноеОкно.Ширина;
		ЭлементФормыКартинка.Высота = ГлавноеОкно.Высота;

		Изображение = Интерфейс.Картинка(, ЭлементФормыКартинка.Размер);
		Холст = ЭлементФормыКартинка.СоздатьГрафику().ИзИзображения(Изображение);
		//Изображение.СделатьПрозрачным(Интерфейс.Цвет().Белый);

	КонецЕсли;

	Возврат ГлавноеОкно;

КонецФункции

Процедура ОтрисоватьЭкран(ИгровыеОбъекты) Экспорт

	Холст.Очистить(ЭлементФормыКартинка.ЦветФона);

    Для Каждого ТекущийОбъектКЗ из ИгровыеОбъекты Цикл
        ТекущийОбъект = ТекущийОбъектКЗ.Значение;
        Если ТекущийОбъект.Видимость = Истина Тогда
            //отрисовка анимации -- -- --

			КартинкаОбъекта = ТекущийОбъект.ПолучитьКадр();

			Если НЕ КартинкаОбъекта = Неопределено Тогда
				Холст.РисоватьИзображение(КартинкаОбъекта,
										ТекущийОбъект.КоординатаХ, 
										ТекущийОбъект.КоординатаУ, 
										ТекущийОбъект.Ширина, 
										ТекущийОбъект.Высота);
				ЭлементФормыКартинка.Изображение = Изображение;
			КонецЕсли;

            Если ТекущийОбъект.ОтображатьГраницы = Истина Тогда
 
                Перо = Интерфейс.Перо(Интерфейс.Цвет().Зеленый, ТекущийОбъект.ТолщинаГраницы);
                Холст.РисоватьПрямоугольник(Перо, ТекущийОбъект.КоординатаХ-1, 
                                                    ТекущийОбъект.КоординатаУ-1, 
                                                    ТекущийОбъект.Ширина + 1, 
                                                    ТекущийОбъект.Высота + 1);
				ЭлементФормыКартинка.Изображение = Изображение;         
                
            КонецЕсли;
        КонецЕсли;
    КонецЦикла;  
    
КонецПроцедуры