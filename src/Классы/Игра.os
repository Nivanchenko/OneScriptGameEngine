
&Пластилин
Перем КонтекстПриложения Экспорт;

Перем Графика Экспорт;
Перем Интерфейс Экспорт;
Перем СкоростьИгровогоЦикла Экспорт;
Перем ИграРаботает Экспорт;
Перем ГлавноеОкно Экспорт;
Перем ИгровыеОбъекты Экспорт;
Перем ОбновитьКадр Экспорт;
Перем ОтрисовкаВФоне Экспорт;
Перем ЗадержкаМеждуКадрами Экспорт;
Перем ОбработчикСобытийИнтерфейса;
Перем ОбработчикИгровогоЦикла;
Перем ОбработчикиНажатий;
Перем ОтложеныеИгровыеСобытия;
Перем ПодпискиНаИгровойЦикл;
Перем ОбработчикВвода;

&Желудь
Процедура ПриСозданииОбъекта(
                            &Пластилин("Графика") _Графика,
                            &Пластилин("ИгровыеОбъекты") _ИгровыеОбъекты,
                            &Пластилин("ПодпискиНаИгровойЦикл") _ПодпискиНаИгровойЦикл,
                            &Пластилин("ОтложеныеИгровыеСобытия") _ОтложеныеИгровыеСобытия,
                            &Пластилин("ОбработчикВвода") _ОбработчикВвода
                            )

    ИгровыеОбъекты = _ИгровыеОбъекты;
    Графика = _Графика;
    ПодпискиНаИгровойЦикл = _ПодпискиНаИгровойЦикл;
    ОтложеныеИгровыеСобытия = _ОтложеныеИгровыеСобытия;
    ОбработчикВвода = _ОбработчикВвода;

    ИнициализацияПараметровИгры();

КонецПроцедуры

Процедура ИнициализацияПараметровИгры()
    
    Интерфейс = Графика.Интерфейс;
    СкоростьИгровогоЦикла = 500;
    ЗадержкаМеждуКадрами = 500;
    ОтрисовкаВФоне = Ложь;
    ОбновитьКадр = Истина;

КонецПроцедуры

Процедура ПодписатьНаИгровойЦикл(ИгровойОбъект, Делегат, ДопПараметры = Неопределено, ВФоне = Ложь) Экспорт

    ПодпискиНаИгровойЦикл.Подписать(ИгровойОбъект, Делегат, ДопПараметры, ВФоне);
    
КонецПроцедуры

Процедура УдалитьПодпискуНаИгровойЦикл(ИгровойОбъект) Экспорт

    ПодпискиНаИгровойЦикл.УдалитьПодписку(ИгровойОбъект);    

КонецПроцедуры

Процедура ОбработатьПодпискиНаИгровойЦикл() Экспорт
    
    ПодпискиНаИгровойЦикл.ОбработатьПодписки();

КонецПроцедуры

Функция СоздатьФизику() Экспорт
    
    Возврат КонтекстПриложения.ПолучитьЖелудь("Физика");

КонецФункции

Функция СоздатьАнимацию(ПутьКФайлу = Неопределено, ЦветПрозрачности = Неопределено) Экспорт
    Анимация = КонтекстПриложения.ПолучитьЖелудь("Анимация");

    Если НЕ ПутьКФайлу = Неопределено Тогда
        Анимация.ДобавитьКадр(ПутьКФайлу, ЦветПрозрачности);
    КонецЕсли;

    Возврат Анимация;
    
КонецФункции

Функция Цвет(Красный, Зеленый, Синий) Экспорт
	Возврат Графика.Цвет(Красный, Зеленый, Синий);	
КонецФункции

Функция ДобавитьИгровойОбъект(_ТипОбъекта = "", _СлойОтрисовки = 0, _СлойФизики = 0) Экспорт

    Возврат ИгровыеОбъекты.Добавить(_ТипОбъекта, _СлойОтрисовки, _СлойФизики);

КонецФункции

Процедура УдалитьИгровойОбъект(ИгровойОбъект) Экспорт
    УдалитьПодпискуНаИгровойЦикл(ИгровойОбъект);
    ИгровыеОбъекты.Удалить(ИгровойОбъект.Идентификатор());
КонецПроцедуры

Процедура УдалитьИгровойОбъектОтложено(ИгровойОбъект) Экспорт
    ДобавитьОтложенноеИгровоеСобытие(Новый Действие(ЭтотОбъект, "УдалитьИгровойОбъект"), 0, ИгровойОбъект);
КонецПроцедуры

Процедура ДобавитьНастройкуНажатий(КодКнопки, ДействиеПриНажатии = Неопределено, ДействиеПриОсвобождении = Неопределено) Экспорт

    ОбработчикВвода.Добавить(КодКнопки, ДействиеПриНажатии, ДействиеПриОсвобождении);
    
КонецПроцедуры

Процедура ДобавитьОтложенноеИгровоеСобытие(Делегат, ЗадержкаВИгровыхЦиклах = 0, ДопПараметры = Неопределено, ВФоне = Ложь) Экспорт

    ОтложеныеИгровыеСобытия.Добавить(Делегат, ЗадержкаВИгровыхЦиклах, ДопПараметры, ВФоне);

КонецПроцедуры

Процедура ОбработатьОтложенныеИгровыеСобытия() Экспорт
     
    ОтложеныеИгровыеСобытия.Обработать();

КонецПроцедуры

Процедура УстановитьОбработчикСобытийИнтерфейса(Делегат) Экспорт

    ОбработчикСобытийИнтерфейса = Делегат;
    
КонецПроцедуры

Процедура УстановитьОбработчикИгровогоЦикла(Делегат) Экспорт

    ОбработчикИгровогоЦикла = Делегат;
    
КонецПроцедуры

Процедура ИгровойЦикл() Экспорт

    Пока ИграРаботает Цикл
        ОбработатьПодпискиНаИгровойЦикл();
        ОбработатьОтложенныеИгровыеСобытия();
        Если не ОбработчикИгровогоЦикла = Неопределено Тогда 
            ОбработчикИгровогоЦикла.Выполнить();
        КонецЕсли;
        Если ОбновитьКадр И не ОтрисовкаВФоне Тогда
            Графика.ОтрисоватьЭкран();
        КонецЕсли;
        Приостановить(СкоростьИгровогоЦикла);
    КонецЦикла
    
КонецПроцедуры

Процедура ФоноваяОтрисовка() Экспорт
    Пока ОтрисовкаВФоне = Истина Цикл
        Если ОбновитьКадр Тогда
            Графика.ОтрисоватьЭкран();
        КонецЕсли;
        Если ЗадержкаМеждуКадрами > 0 Тогда
            Приостановить(ЗадержкаМеждуКадрами);
        КонецЕсли;
    КонецЦикла;
КонецПроцедуры

Процедура ЗапуститьВФоне(Делегат, ДопПараметры = Неопределено, Ждать = 0) Экспорт
    Параметры = Новый Массив;
    Параметры.Добавить(Делегат);
    Параметры.Добавить(ДопПараметры);
    Параметры.Добавить(Ждать);
    ФоновыеЗадания.Выполнить(ЭтотОбъект, "ФоновыйЗапускДелегата", Параметры)
КонецПроцедуры

Процедура ФоновыйЗапускДелегата(Делегат, ДопПараметры = Неопределено, Ждать = 0) Экспорт

    Если Ждать > 0  Тогда
        Приостановить(Ждать);
    КонецЕсли;

    Если ДопПараметры = Неопределено Тогда
        Делегат.Выполнить();    
    Иначе
        Делегат.Выполнить(ДопПараметры);
    КонецЕсли;

КонецПроцедуры

Процедура ОстановитьИгровойЦикл() Экспорт
    ИграРаботает = Ложь;    
КонецПроцедуры

Процедура ЗапуститьОбработчикВвода() Экспорт

    ГлавноеОкно.КлавишаВниз = "КлавишаВниз";
    ГлавноеОкно.КлавишаВверх = "КлавишаВверх";

    Пока Интерфейс.Продолжать Цикл
        Событие = СтрЗаменить(Интерфейс.ПолучитьСобытие(), "()","");
        Отправитель = Интерфейс.Отправитель;
        Если Событие = "КлавишаВниз" ИЛИ Событие = "КлавишаВверх" Тогда 
            Клавиша = Интерфейс.КлавишаАрг();
            СобытияКнопки = ОбработчикВвода.ПолучитьСобытие(Клавиша.КодКлавиши);
            Если НЕ СобытияКнопки = Неопределено Тогда
                ДелегатСобытия = СобытияКнопки[Событие];
                Если не ДелегатСобытия = Неопределено Тогда
                    ДелегатСобытия.Выполнить();
                КонецЕсли;
            КонецЕсли;
        ИначеЕсли не ОбработчикСобытийИнтерфейса = Неопределено Тогда
            ОбработчикСобытийИнтерфейса.Выполнить(Отправитель, Событие);
        КонецЕсли
    КонецЦикла;

КонецПроцедуры

Процедура ЗапуститьПриложение(Заголовок = "", ШиринаОкна = 800, ВысотаОкна = 600) Экспорт
    
    ИграРаботает = Истина;

    ГлавноеОкно = Графика.ГлавноеОкно();

    ЗапуститьВФоне(Новый Действие(ЭтотОбъект, "ИгровойЦикл"));
    Если ОтрисовкаВФоне Тогда
        ЗапуститьВФоне(Новый Действие(ЭтотОбъект, "ФоноваяОтрисовка"));    
    КонецЕсли;
    ЗапуститьОбработчикВвода();
    
КонецПроцедуры
