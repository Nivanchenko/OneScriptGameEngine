
Перем Графика Экспорт;
Перем Интерфейс Экспорт;
Перем СкоростьИгровогоЦикла Экспорт;
Перем ИграРаботает Экспорт;
Перем ОснованяФормаПриложения Экспорт;
Перем ИгровыеОбъекты Экспорт;
Перем ОбновитьКадр Экспорт;
Перем ОбработчикСобытий;
Перем ОбработчикИгровогоЦикла;
Перем ОбработчикиНажатий;
Перем Холст;



Процедура ПриСозданииОбъекта()
	
    Графика = Новый Графика();

	Интерфейс = Графика.Интерфейс;

    СкоростьИгровогоЦикла = 500;

    ОбработчикиНажатий = Новый Соответствие();  
    
    ИгровыеОбъекты = Новый Соответствие();

    ОбновитьКадр = Истина;

КонецПроцедуры

Функция СортировкаСоотвествия(Соответствие, РеквизитСортировки) Экспорт
    СЗ = Новый СписокЗначений();
    Для Каждого КиЗ Из Соответствие Цикл
        СЗ.Добавить(КиЗ.Значение, КиЗ.Значение[РеквизитСортировки]);
    КонецЦикла;
    СЗ.СортироватьПоПредставлению();
    Возврат СЗ;
КонецФункции

Функция ДобавитьИгровойОбъект(_ТипОбъекта = "", _СлойОтрисовки = 0, _СлойФизики = 0) Экспорт

    НовыйИгровойОбъект = Новый ИгровойОбъект(_ТипОбъекта, _СлойОтрисовки, _СлойФизики);

    ИгровыеОбъекты.Вставить(НовыйИгровойОбъект.Идентификатор(), НовыйИгровойОбъект);
    
    Возврат НовыйИгровойОбъект;

КонецФункции

Процедура ДобавитьНастройкуНажатий(КодКнопки, ДействиеПриНажатии, ДействиеПриОсвобождении) Экспорт

    ОбработчикиНажатий.Вставить(КодКнопки, Новый Структура("КлавишаВниз, КлавишаВверх", ДействиеПриНажатии, ДействиеПриОсвобождении));
    
КонецПроцедуры

Процедура УстановитьОбработчикСобытий(Делегат) Экспорт

    ОбработчикСобытий = Делегат;
    
КонецПроцедуры

Процедура УстановитьОбработчикИгровогоЦикла(Делегат) Экспорт

    ОбработчикИгровогоЦикла = Делегат;
    
КонецПроцедуры

Процедура ИгровойЦикл() Экспорт

    Пока ИграРаботает Цикл
        Если не ОбработчикИгровогоЦикла = Неопределено Тогда 
            ОбработчикИгровогоЦикла.Выполнить();
        КонецЕсли;
        ОтрисоватьЭкран();
        Приостановить(СкоростьИгровогоЦикла);
    КонецЦикла
    
КонецПроцедуры

Процедура ЗапуститьИгровойЦикл() Экспорт
    ФоновыеЗадания.Выполнить(ЭтотОбъект, "ИгровойЦикл");
КонецПроцедуры

Процедура ОстановитьИгровойЦикл() Экспорт
    ИграРаботает = Ложь;    
КонецПроцедуры

Процедура ЗапуститьПриложение(Заголовок = "", ШиринаОкна = 800, ВысотаОкна = 600) Экспорт
    ИграРаботает = Истина;

    ОснованяФормаПриложения = Интерфейс.Форма();
    ОснованяФормаПриложения.МаксимальныйРазмер = Интерфейс.Размер(ШиринаОкна, ВысотаОкна);
    ОснованяФормаПриложения.Высота = ВысотаОкна;
    ОснованяФормаПриложения.Ширина = ШиринаОкна;
    ОснованяФормаПриложения.Текст = Заголовок;
    ОснованяФормаПриложения.Отображать = Истина;
    ОснованяФормаПриложения.Показать();
    ОснованяФормаПриложения.Активизировать();
    ОснованяФормаПриложения.ДвойнаяБуферизация = Истина;

    ОснованяФормаПриложения.ЦветФона = Интерфейс.Цвет().Красный;
    ОснованяФормаПриложения.ПрозрачныйЦвет = ОснованяФормаПриложения.ЦветФона;

    ОснованяФормаПриложения.КлавишаВниз = "КлавишаВниз";
    ОснованяФормаПриложения.КлавишаВверх = "КлавишаВверх";

    Холст = ОснованяФормаПриложения.СоздатьГрафику();

    ЗапуститьИгровойЦикл();
     
    Пока Интерфейс.Продолжать Цикл
        Событие = Стрзаменить(Интерфейс.ПолучитьСобытие(), "()", "");
        Отправитель = Интерфейс.Отправитель;
        Если Событие = "КлавишаВниз" ИЛИ Событие = "КлавишаВверх" Тогда 
            Клавиша = Интерфейс.КлавишаАрг();
            СобытияКнопки = ОбработчикиНажатий[Клавиша.КодКлавиши];
            Если НЕ СобытияКнопки = Неопределено Тогда
                СобытияКнопки[Событие].Выполнить();
            КонецЕсли;
        ИначеЕсли не ОбработчикСобытий = Неопределено Тогда
            ОбработчикСобытий.Выполнить(Отправитель, Событие);
        КонецЕсли
    КонецЦикла;
КонецПроцедуры

Процедура ОтрисоватьЭкран() Экспорт
    Если ОбновитьКадр = Ложь Тогда
        Возврат;
    КонецЕсли;

    Холст.Очистить(ОснованяФормаПриложения.ЦветФона);
    Для Каждого ТекущийОбъектКЗ из СортировкаСоотвествия(ИгровыеОбъекты, "СлойОтрисовки") Цикл
        ТекущийОбъект = ТекущийОбъектКЗ.Значение;
        Если ТекущийОбъект.Видимость = Истина Тогда
            //отрисовка анимации -- -- --
            Если ТекущийОбъект.ОтображатьГраницы = Истина Тогда
 
                Перо = Интерфейс.Перо(Интерфейс.Цвет().Зеленый, ТекущийОбъект.ТолщинаГраницы);
                Холст.РисоватьПрямоугольник(Перо, ТекущийОбъект.КоординатаХ-1, 
                                                    ТекущийОбъект.КоординатаУ-1, 
                                                    ТекущийОбъект.Ширина + 1, 
                                                    ТекущийОбъект.Высота + 1);         
                
            КонецЕсли;
        КонецЕсли;
    КонецЦикла;  

    ОбновитьКадр = Ложь;
    
КонецПроцедуры
