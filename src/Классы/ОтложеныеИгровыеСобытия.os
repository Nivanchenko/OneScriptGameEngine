Перем КоллекцияСобытий;

&Желудь
Процедура ПриСозданииОбъекта()

	ИнициализацияКоллекции();
	
КонецПроцедуры

Процедура Добавить(Делегат, ЗадержкаВИгровыхЦиклах = 0, ДопПараметры = Неопределено, ВФоне = Ложь) Экспорт
    НовоеСобытие = КоллекцияСобытий.Добавить();
    НовоеСобытие.Делегат = Делегат;
    НовоеСобытие.СчетчикСобытий = ЗадержкаВИгровыхЦиклах; 
    НовоеСобытие.ДопПараметры = ДопПараметры;
    НовоеСобытие.ВФоне = ВФоне; 
    НовоеСобытие.Новый = Истина;
КонецПроцедуры

Процедура Обработать() Экспорт
    ОтработанныеСобытия = Новый Массив;

    ТекущиеСтрокиКОбработке = КоллекцияСобытий.НайтиСтроки(Новый Структура("Новый", Ложь));

    Для каждого ТекСобытие Из ТекущиеСтрокиКОбработке Цикл
        Если ТекСобытие.СчетчикСобытий < 1 Тогда
            Если ТекСобытие.ДопПараметры = Неопределено Тогда
                ТекСобытие.Делегат.Выполнить();
            Иначе
                ТекСобытие.Делегат.Выполнить(ТекСобытие.ДопПараметры);   
            КонецЕсли;
            ОтработанныеСобытия.Добавить(ТекСобытие);
        Иначе
            ТекСобытие.СчетчикСобытий = ТекСобытие.СчетчикСобытий - 1;    
        КонецЕсли;    
    КонецЦикла;

    Для каждого СобытиеУдалить Из ОтработанныеСобытия Цикл
        КоллекцияСобытий.Удалить(СобытиеУдалить);
    КонецЦикла;

    Для каждого ТекСобытие Из КоллекцияСобытий Цикл
        ТекСобытие.Новый = Ложь;
    КонецЦикла;
КонецПроцедуры

Процедура ИнициализацияКоллекции()
    
	КоллекцияСобытий = Новый ТаблицаЗначений();
    КоллекцияСобытий.Колонки.Добавить("СчетчикСобытий");
    КоллекцияСобытий.Колонки.Добавить("Делегат");
    КоллекцияСобытий.Колонки.Добавить("ВФоне");
    КоллекцияСобытий.Колонки.Добавить("ДопПараметры");
    КоллекцияСобытий.Колонки.Добавить("Новый"); 
    
КонецПроцедуры