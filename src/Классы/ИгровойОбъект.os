Перем Идентификатор;
Перем Игра;

Перем ТипОбъекта Экспорт;
Перем Высота Экспорт;
Перем Ширина Экспорт;
Перем КоординатаХ Экспорт;
Перем КоординатаУ Экспорт;
Перем ЭтоИгрок Экспорт;
Перем Свойства Экспорт;
Перем Действия Экспорт;
Перем СлойОтрисовки Экспорт;
Перем СлойФизики Экспорт;
Перем ОтображатьГраницы Экспорт;
Перем ТолщинаГраницы Экспорт;
Перем Видимость Экспорт;

Перем Анимация;
Перем НомерКадра Экспорт;
Перем ПереключатьКадры Экспорт;
Перем ЗадержкаМеждуКадрами Экспорт;
Перем СчетчикКадров;

Процедура ПриСозданииОбъекта(_Игра, _ТипОбъекта = "", _СлойОтрисовки = 0, _СлойФизики = 0)

	Игра = _Игра;

	ОтображатьГраницы = Ложь;
	ТолщинаГраницы = 2;
	Видимость = Ложь;

	ТипОбъекта = _ТипОбъекта;
	СлойФизики = _СлойФизики;
	СлойОтрисовки = _СлойОтрисовки;

	Свойства = Новый Соответствие();
	Действия = Новый Соответствие();

	Идентификатор = Строка(Новый УникальныйИдентификатор());
	
КонецПроцедуры

Процедура Удалить() Экспорт
	Игра.УдалитьИгровойОбъект(ЭтотОбъект);	
КонецПроцедуры

Процедура УдалитьОтложено() Экспорт
	Игра.УдалитьИгровойОбъектОтложено(ЭтотОбъект);	
КонецПроцедуры

Процедура ДобавитьСобытие(ИмяДействия, Действие) Экспорт

	Действия.Вставить(ИмяДействия, Действие);
	
КонецПроцедуры

Процедура ВыполнитьДействие(ИмяДействия) Экспорт
	ИскомоеДействие = Действия[ИмяДействия];
	Если ИскомоеДействие = Неопределено Тогда
		ВызватьИсключение СтрШаблон("Действие %1 не определено", ИмяДействия);
	Иначе
		ИскомоеДействие.Выполнить(ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры

Процедура УстановитьСвойство(ИмяСвойства, Значение = Неопределено) Экспорт
	Свойства.Вставить(ИмяСвойства, Значение);	
КонецПроцедуры

Функция ПолучитьСвойство(ИмяСвойства) Экспорт
	Возврат Свойства[ИмяСвойства];	
КонецФункции

Функция Идентификатор() Экспорт
	Возврат Идентификатор;
КонецФункции

Процедура УстановитьАнимацию(_Анимация, _ЗадержкаМеждуКадрами = 0) Экспорт
	Анимация = _Анимация;
	НомерКадра = 0;
	ПереключатьКадры = Истина;
	ЗадержкаМеждуКадрами = _ЗадержкаМеждуКадрами;
	СчетчикКадров = 0;
КонецПроцедуры

Функция ПолучитьКадр() Экспорт

	Если Анимация = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	Кадр = Анимация.ПолучитьКадр(НомерКадра);
	Если ПереключатьКадры = Истина Тогда
		Если СчетчикКадров >= ЗадержкаМеждуКадрами Тогда
			НомерКадра = НомерКадра + 1;
			СчетчикКадров = 0;
		КонецЕсли;
	КонецЕсли;
	СчетчикКадров = СчетчикКадров + 1;
	Возврат Кадр;
КонецФункции